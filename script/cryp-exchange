#!perl

# FRAGMENT id=shcompgen-hint command=cryp-exchange

# DATE
# VERSION

use 5.010001;
use strict;
use warnings;
use Log::ger;

use Perinci::CmdLine::cryp;
use PERLANCAR::Module::List;

my $prefix = "/App/cryp/exchange/";
my $mods = do {
    # if we are being called during the building of our own dist by dzil (e.g.
    # by Pod::Weaver::Plugin::Rinci), don't include subcommands installed
    # outside our dist. otherwise we'll be documenting and depending on
    # extraneous subcommands.
    local @INC = ("lib")
        if $ENV{DZIL} &&
            # DZP:SetEnv always sets DZIL=1 so we need to know if it's our own
            # dist that we're building
        (!defined($ENV{DZIL_NAME}) ||
         $ENV{DZIL_NAME} eq 'App-cryp-exchange');

    PERLANCAR::Module::List::list_modules(
        "App::cryp::Cmd::Exchange::", {list_modules=>1});
};
my $subcommands = {};
for my $mod (keys %$mods) {
    (my $sc_name = $mod) =~ s/App::cryp::Cmd::Exchange:://;
    $sc_name =~ s/_/-/g;

    (my $url = $mod) =~ s!::!/!g;
    $subcommands->{$sc_name} = {
        url => "/$url/handle_cmd",
    };
}

Perinci::CmdLine::cryp->new(
    url => $prefix,
    log => 1,
    pass_cmdline_object => 1,
    config_filename => ["cryp.conf", "cryp-exchange.conf"],
    subcommands => $subcommands,
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

Create configuration in F<~/.config/cryp-exchange.conf> (or
F<~/.config/cryp.conf> so configuration can be shared with the other C<cryp-*>
family of scripts):

 [exchange/bitcoin-indonesia]
 api_key    = S6WBYR6Q-TVMTSFOA-YWK6GSNT-H0JMEK6Z-YDF5ML9Y
 api_secret = e16064aa7fb477b267fe774df62194f33583c7f46a09ad931ca2e097aeaa9c9296e1c3b2734bab71

 ; if you have another account on the same exchange
 [exchange/bitcoin-indonesia/wifey]
 api_key    = EKSGJUOR-ROR9K24P-Q45UBWVG-GZHANPQV-UY4Z1WBM
 api_secret = 71100dc13ffe4d082239c06b5c36fd8f0548cd97056ffa2ee491ae4dfd4f996fb468ef3c70b12073

 [exchange/gdax]
 api_key        = 2171d01f1de2518c94a11870a6df22d0
 api_secret     = 9B8T2nPFMRUiUIKA82txjaO09iNnJXBMVMdkDgJ0i2YambYKQY8DZAS54ERZE0sE2lzYrpTWfpU7/110xfesZluY
 api_passphrase = gdllszgwO774NN

To list supported exchanges:

 % cryp-exchange exchanges
 % cryp-exchange exchanges -l

To list pairs offered in an exchange:

 % cryp-exchange pairs indodax
 % cryp-exchange pairs gdax/wife -l

To list accounts:

 % cryp-exchange accounts
 % cryp-exchange accounts -l
 % cryp-exchange accounts -I gdax -l ;# only accounts on gdax
 % cryp-exchange accounts -X gdax -l ;# all accounts except those on gdax

To get order book:

 % cryp-exchange orderbook gdax

To get account balances:

 % cryp-exchange balance gdax
 % cryp-exchange balance gdax/wife


=head1 DESCRIPTION

B<Early release. More functionality will be added in the future.>

cryp-exchange is a multi-cryptoexchange CLI. This distribution provides common
interface (as CLI as well as driver modules) to interact with different
cryptocurrency exchanges including:

=over

=item * common set of subcommands (API methods)

Along with common terminology, arguments, error behavior, and result format.

=item * standardized currency codes and pair formats

Following L<CryptoCurrency::Catalog>.

=back


=head1 SEE ALSO

The other C<cryp-*> scripts in the family.

L<CryptoCurrency::Catalog>

L<CryptoExchange::Catalog>
